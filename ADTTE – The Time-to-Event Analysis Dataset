*----------------------------------------------------------------*;
* ADTTE.sas creates the ADaM BDS-structured data set for a *;
* time-to-event analysis (ADTTE), saved to the ADaM libref. *;
*----------------------------------------------------------------*;
**** CREATE EMPTY ADTTE DATASET CALLED EMPTY_ADTTE;
options mprint ;
%let metadatafile=&path/data/adam-metadata/adam_metadata.xlsx
%make_empty_dataset(metadatafile=&metadatafile,dataset=ADTTE)
proc sort
 data = adam.adsl
 (keep = studyid usubjid siteid country age agegr1 agegr1n sex race 
randdt trt01p trt01pn ittfl trtedt)
 out = adtte;
 by usubjid;
proc sort
 data = adam.adef
 (keep = usubjid paramcd chg adt visitnum xpseq)
 out = adef;
 where paramcd='XPPAIN' and visitnum>0 and abs(chg)>0;
 by usubjid adt;
data adef;
 set adef;
 by usubjid adt;
 drop paramcd visitnum;
 if first.usubjid; 
run;
proc sort
 data = adam.adae
 (keep = usubjid CQ01NAM astdt trtemfl aeseq)
 out = adae;
 where CQ01NAM ne '' and trtemfl='Y';
 by usubjid astdt;
run;
** keep only the first occurence of a pain event;
data adae;
 set adae;
 by usubjid aesdt;
 
 if first.usubjid; 
run; 
data adtte;
 merge adtte (in = inadtte rename=(randdt=startdt)) 
 adef (in = inadef) 
 adae (in = inadae)
 ; 
 by usubjid ;
 retain param "Time to first pain relief (days)" 
 paramcd "TTPNRELF"
 ; 
 rename trt01p = trtp
 trt01pn = trtpn
 ; 
 length srcvar $10. srcdom $4.;
 if (.<chg<0) and (adt<astdt or not inadae) then 
 do;
 ** ACTUAL PAIN RELIEF BEFORE WORSENING;
 cnsr = 0;
 adt = adt;
 evntdesc = put(cnsr, evntdesc.) ;
 srcdom = 'ADEF';
 srcvar = 'ADY';
 srcseq = xpseq;
 end;
 else if chg>0 and (adt<astdt or not inadae) then
 do;
 ** CENSOR: PAIN WORSENING BEFORE RELIEF;
 cnsr = 1;
 adt = adt;
 evntdesc = put(cnsr, evntdesc.) ;
 srcdom = 'ADEF';
 srcvar = 'ADY';
 srcseq = xpseq;
 end;
 else if (.<astdt<adt) then
 do;
 ** CENSOR: PAIN AE BEFORE RELIEF; 
 cnsr = 2;
 adt = astdt;
 evntdesc = put(cnsr, evntdesc.) ;
 srcdom = 'ADAE'; 
 srcvar = 'ASTDY';
 srcseq = aeseq;
 end;
 else 
 do;
 ** CENSOR: COMPLETED STUDY BEFORE PAIN RELIEF OR WORSENING;
 cnsr = 3; 
 adt = trtedt;
 evntdesc = put(cnsr, evntdesc.) ;
 srcdom = 'ADSL';
 srcvar = 'TRTEDT';
 srcseq = .;
 end;
 aval = adt - startdt + 1;
 format adt yymmdd10.;
run;
** assign variable order and labels;
data adtte;
 retain &adtteKEEPSTRING;
 set EMPTY_adtte adtte;
run;
**** SORT adtte ACCORDING TO METADATA AND SAVE PERMANENT DATASET;
%make_sort_order(metadatafile=&metadatafile, dataset=ADTTE)
proc sort
 data=adtte(keep = &adtteKEEPSTRING)
 out=adam.adtte;
 by &adtteSORTSTRING;
run; 
