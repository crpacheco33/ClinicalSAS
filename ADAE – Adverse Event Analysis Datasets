*------------------------------------------------------------*;
* ADAE.sas creates the ADaM ADAE-structured data set *;
* for AE data (ADAE), saved to the ADaM libref. *;
*------------------------------------------------------------*;
**** CREATE EMPTY ADAE DATASET CALLED EMPTY_ADAE;
%let metadatafile=&path/data/adam-metadata/adam_metadata.xlsx
%make_empty_dataset(metadatafile=&metadatafile,dataset=ADAE)
proc sort
 data = adam.adsl
 (keep = usubjid siteid country age agegr1 agegr1n sex race trtsdt trt01a 
trt01an saffl) 
 out = adsl;
 by usubjid;
data adae;
 merge sdtm.ae (in = inae) adsl (in = inadsl);
 by usubjid ;
 if inae and not inadsl then
 put 'PROB' 'LEM: Subject missing from ADSL?-- ' usubjid= inae= 
 inadsl= ;
 length CQ01NAM $40.;
 rename trt01a = trta 
 trt01an = trtan
 ; 
 if inadsl and inae;
 %dtc2dt(aestdtc, prefix=ast, refdt=trtsdt); 
 %dtc2dt(aeendtc, prefix=aen, refdt=trtsdt);
 if index(upcase(AEDECOD), 'PAIN')>0 or upcase(AEDECOD)='HEADACHE' then 
 CQ01NAM = 'PAIN EVENT';
 else
 CQ01NAM = ' ';
 aereln = input(put(aerel, $aereln.), best.);
 aesevn = input(put(aesev, $aesevn.), best.);
 relgr1n = (aereln>0); ** group related events (AERELN>0);
 relgr1 = put(relgr1n, relgr1n.);
 
 * Event considered treatment emergent if it started on or after;
 * the treatment start date. Assume treatment emergent if start;
 * date is missing (and the end date is either also missing or >=;
 * the treatment start date) ;
trtemfl = put((astdt>=trtsdt or (astdt<=.z and 
not(.z<aendt<trtsdt))), _0n1y.);
run;
** assign variable order and labels;
data adae;
 retain &adaeKEEPSTRING;
 set EMPTY_adae adae;
run;
**** SORT adae ACCORDING TO METADATA AND SAVE PERMANENT DATASET;
%make_sort_order(metadatafile=&metadatafile, dataset=ADAE)
proc sort
 data=adae(keep = &adaeKEEPSTRING)
 out=adam.adae;
 by &adaeSORTSTRING;
run; 
